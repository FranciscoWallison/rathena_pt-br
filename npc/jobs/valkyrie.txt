//===== rAthena Script ======================================= 
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 3.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions,additional checks,quest skills,nowà Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=     Removed all functions as they are no longer required.
//=     Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=     scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//= 3.0 Added quest log commands. [L0ne_W0lf]
//= 3.1 Added Izlude Renewal coordinates.
//===================== Tradução PT-BR ==========
// Tradução: HanzoBR
// Revisado: HD Scripts - Projeto eaBrasil
//=============================================== 

valkyrie,48,86,4	script	Valkyrie#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "Bem vindo";
		mes "Ao salão de Honra de Valhalla.";
		next;
		mes "[Valkyrie]";
		mes "Por favor, sinta-se a vontade enquanto";
		mes "Estiver por aqui.";
		mes "Honra aos Guerreiros!";
		close;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "Bem vindo";
		mes "Ao salão de Honra de Valhalla.";
		next;
		mes "[Valkyrie]";
		mes "Dê Adeus a sua vida atual e curta";
		mes "a sua nova vida a partir de agora.";
		mes "Honra aos Guerreiros!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[Valkyrie]";
			mes "Você deve fazer algumas coisas antes de começarmos.";
			mes "Primeiro você tem que esvaziar sua mente e alma.";
			mes "Conseguindo isto você se livra de todos os pensamentos egoístas";
			mes "e consegue alcançar à Honra!";
			next;
			mes "[Valkyrie]";
			mes "Você não poderá carregar nada com você para sua próxima vida";
			mes "Zeny, Pets e Carrinhos deverem ser deixados para trás.";
			next;
			mes "[Valkyrie]";
			mes "Quando você estiver pronto retorne para mim";
			mes "estarei te aguardando aventureiro.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "Estou impressionada "+ strcharinfo(0) +".";
			mes "Vejo que se libertou dos desejos";
			mes "mais sujos que podem existir";
			next;
			mes "[Valkyrie]";
			mes "O que você fez é muito admirável ainda mais por ser um jovem aventureiro.";
			mes "Você conseguiu compreender que a Honra só vem quando você esquece seus desejos";
			mes "pessoais em prol do bem maior da Humanidade.";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "Consigo sentir, você ainda tem algum";
				mes "apego com sua vida atual, cuide disto.";
				mes "Retorne a me procurar quando estiver pronto!";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "Agora irei remover todas";
			mes "as suas memórias desta vida";
			mes "Não se preocupe, os melhores";
			mes "momentos você se lembrará";
			next;
			mes "[Valkyrie]";
			mes "Com Um,";
			mes "Pedirei à Deusa Urd para remover";
			mes "suas memórias sujas desta vida.";
			next;
			mes "[Valkyrie]";
			mes "Com Dois,";
			mes "Pedirei à Deusa Verdandi para que";
			mes "mantenha memórias honrosas desta vida.";
			next;
			mes "[Valkyrie]";
			mes "Com Três,";
			mes "Pedirei à Deusa Skuld para que";
			mes "lhe guie em sua próxima vida.";
			next;
			mes "[Valkyrie]";
			mes "Um...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "Um...";
			mes "Dois......";
			next;
			mes "[Valkyrie]";
			mes "Um...";
			mes "Dois......";
			mes "E Três.";
			set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST & ~1024;//<-reset Skill Reset Event
			skill "NV_FIRSTAID",1,SKILL_PERM;
			skill "NV_TRICKDEAD",1,SKILL_PERM;
			completequest 1000;
			next;
			mes "[Valkyrie]";
			mes "Parabéns!";
			mes "Você está renascido em uma nova vida.";
			mes "Por favor, pegue estes pequenos presentes para sua nova jornada.";
			getitem 1202,1; //Knife_
			getitem 2302,1; //Cotton_Shirt_
			next;
			mes "[Valkyrie]";
			mes "Espero que a libertação que a Deusa Urd fez lhe prove ser uma bênção";
			mes "e que as memórias que a Verdandi deixou lhe honre sempre.";
			next;
			mes "[Valkyrie]";
			mes "Espero que nesta nova vida a qual Deusa Skuld irá lhe guiar seja ainda mais honrosa.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (checkre(0)) warp "izlude",129,97;
				else warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "Bem vindo";
		mes "Ao salão de Honra de Valhalla.";
		next;
		mes "[Valkyrie]";
		mes "Infelizmente você ainda não foi convidado, por gentileza saia imediatamente.";
        mes "Honra aos Guerreiros!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	Metheus Sylphe#Library	742,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[Metheus Sylphe]";
			mes "Bem vindo à Biblioteca da Academia de Magia Schweicherbil.";
			mes "Acredito que tenha vindo para ler o 'Livro de Ymir'";
			next;
			mes "[Metheus Sylphe]";
			mes "Infelizmente a cópia autêntica foi danificada ao longo do tempo. Só podemos que visualizem uma simples cópia do livro.";
			next;
			mes "[Metheus Sylphe]";
			mes "Além de preservar o Original 'Livro de Ymir', decidimos aceitar doações das pessoas que desejam ler nossa cópia simples.";
			next;
			mes "[Metheus Sylphe]";
			mes "Necessitamos de uma doação na quantia de";
			mes "1,285,000 zeny.";
			next;
			if (select("Doar.:Não tenho.") == 1) {
				if (Zeny >= 1285000) {
					set Zeny, Zeny-1285000;
					set valkyrie_Q,1;
					mes "[Metheus Sylphe]";
					mes "Muito obrigado pela sua doação, agora poderá ler o livro.";
					close;
				}
				mes "[Metheus Sylphe]";
				mes "Infelizmente você não tem a quantia que necessitamos, cheque suas finanças e volte.";
				close;
			}
			mes "[Metheus Sylphe]";
			mes "Leve o tempo que precisar e";
			mes "Boa viagem";
			close;
		}
		mes "[Metheus Sylphe]";
		mes "Mais uma vez agradecemos sua doação, fique a vontade para ler uma cópia do 'Livro de Ymir'.";
		close;
	}
	mes "[Metheus Sylphe]";
	mes "Bem vindo à Biblioteca da Academia de Magia Schweicherbil.";
	mes "Aqui temos incontáveis livros, reserve um tempo e dê uma boa olhada ao redor.";
	close;
}

yuno_in02,93,207,1	script	O Livro de Ymir	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[O Livro de Ymir]";
		mes "... A entrada para o Salão de Honra está aberta para qualquer um que tenha avançado em sua próxima vida. Está lá para ajudar heróis a decidir o que querem fazer e pode levá-los a qualquer lugar neste mundo.";
		next;
		mes "[O Livro de Ymir]";
		mes "O Salão de Honra tem tudo preparado para heróis, dizem que qualquer desejo que não possa ser realizado na vida comun, este poderá ser realizado no interior do Salão de Honra.";
		next;
		if (select("Parar de Ler.:Continuar a Ler.") == 1) {
			mes "[O Livro de Ymir]";
			mes ".....";
			close;
		}
		mes "[O Livro de Ymir]";
		mes "Existe um caminho já esquecido que leva ao Salão de Honra, o lugar mais próximo do céu. Quem não for puro jamais conseguirá encontrar este caminho.";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[O Livro de Ymir]";
			mes "...Com isto, os antigos heróis";
			mes "estavam sempre angustiados sabendo";
			mes "que a qualquer momento seriam mortais";
			mes "e passariam deste reino...";
			next;
			mes "[O Livro de Ymir]";
			mes "Não existia nenhum registro, música ou história remanescente sobre a vida após a morte.";
			mes "Mas eu descobri recentemente um pergaminho antigo sobre a Valkyrie.";
			next;
			mes "[O Livro de Ymir]";
			mes "Valkyrie...";
			mes "A legendária,";
			mes "A Guardiã,";
			mes "O Anjo do Ragnarok.";
			next;
			mes "[O Livro de Ymir]";
			mes "Aquele que for de tremenda força e bravura,";
			mes "serão conduzidos pela Valkyrie à Valhalla ao";
			mes "Salão de Honra, onde receberão uma nova vida.";
			next;
			mes "[O Livro de Ymir]";
			mes "Renascidos, eles viverão novamente como";
			mes "Heróis ainda maiores que cuidarão do Mundo";
			mes "Aqueles cujo tiveram energias esgotadas terá";
			mes "suas forças totalmente renovadas.";
			next;
			mes "[O Livro de Ymir]";
			mes "E suas almas receberão habilidades com o Coração de Ymir, porém este foi destruído e fragmentado pelo Mundo inteiro após a épica batalha por Rune-Midgard.";
			next;
			mes "[O Livro de Ymir]";
			mes "Por mais de um século ando a procura,";
			mes "encontrei alguns pedaços do coração de Ymir.";
			mes "Não tenho como confirmar se a história de";
			mes "Valkyrie e Valhalla é verdadeira apenas com a ciência.";
			next;
			mes "[O Livro de Ymir]";
			mes "Assim então deixo este registro acreditando que no futuro algum herói possa confirmar";
			mes "que o Anjo Valkyrie e Valhalla existem.";
			next;
			set Valkyrie_Q,2;
			if(checkquest(1000) == -1) {
				setquest 1000;
			}
			mes "[O Livro de Ymir]";
			mes "Deixe os heróis com suas novas vidas";
			mes "Para que possam proteger o mundo.";
			mes "E depois.......";
			close;
		}
		mes "[O Livro de Ymir]";
		mes "...";
		close;
	}
	mes "[O Livro de Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Heart of Ymir	111,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporte	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleporte]";
			mes "Parabéns.";
			mes "Honra aos Guerreiros!";
			close;
		}
		mes "[Teleporte]";
		mes "Por favor não toque";
		mes "em nenhuma das exposições.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleporte]";
		mes "Bravo Herói";
		mes "Para qual lugar deseja ir?";
		next;
		switch(select("Prontera:Morroc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (checkre(0)) callsub S_Warpchar,"izlude",129,97;
			else callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[Teleporte]";
	mes "Tenha uma ótima viagem.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2),1,1;
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75