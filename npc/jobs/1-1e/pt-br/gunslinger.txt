//===== rAthena Script ======================================= 
//= Gunslinger Job Quest
//===== By: ================================================== 
//= erKURITA, RockmanEXE, Kisuka
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Gunslinger Job Change Quest
//===== Additional Comments: ================================= 
//= 1.0 Made the NPC [erKURITA]
//= 1.5 Couple fixes to the npc, aswell as adding the missing
//=     3 green herbs. [erKURITA]
//= 1.6 job number->const, commonized variable name,
//=     optimized [Lupus]
//= 1.7 Now uses only ONE variable GUNS_Q [Lupus]
//= 1.7a Grammar fixes, few spelling fixes, etc [CBMaster]
//= 1.7b Parenthesis fixes [KarLaeda]
//= 1.8 Fixed the reward you can get [Playtester]
//= 1.9 Removed some 'clever' script constructs [ultramage]
//= 2.0 Updated to official version - Thanks to Omega for
//=     his first try [SinSloth] 1.10b - removed .GATs [Lupus]
//= 2.1 Sábio Chifre de Touro now deletes all items. Thanks to yyCC. [L0ne_W0lf]
//= 2.1a Corrected a Typo error ";;". [Samuray22]
//= 2.1b Corrected some typos errors. (bugreport:1549) [Samuray22]
//= 2.1c Fixed some missing/messed up item checks. (bugreport:2693) [Kisuka]
//= 2.2 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//============================================================

que_ng,152,167,3	script	Mestre Miller	901,{
	if (Upper == 2) {
		mes "[Mestre Miller]";
		mes "[Mestre Miller]";
		mes "Bem, voce nao é uma adorável criancinha?";
		mes "Onde está sua mamae?";
		mes "Esse lugar é perigoso, entao, por favor, vá logo para casa, ok?";
		close;
	}
	if (Class == Job_Novice) {
		if (!callfunc("F_CanChangeJob")) {
			mes "[Mestre Miller]";
			mes "Interessado em se tornar um Justiceiro, eh?";
			mes "Voce tem potencial, mas nao tem experiencia suficiente.";
			mes "Treine mais um pouco, e depois volte, entendeu?";
			close;
		}
		if (GUNS_Q == 0) {
			mes "[Mestre Miller]";
			mes "Eu sou Miller, um treinador de Justiceiros e guardiao em tempo integral de Lady Selena.";
			mes "Pois bem, do que voce precisa?";
			mes "Se nao for importante, entao nao tenho tempo a perder com voce.";
			next;
			if(select("Nada.:Eu quero me tornar um Justiceiro.") == 1) {
				mes "[Mestre Miller]";
				mes "Nao disperdice meu tempo.";
				mes "Se voce quiser se tornar um Justiceiro, volte e fale comigo";
				close;
			}
			mes "[Mestre Miller]";
			mes "Hm. Voce é muito jovem, mas seus olhos dizem que voce é bastante ambicioso.";
			mes "Voce terá que passar em nossa entrevista e curso educacional para se tornar um Justiceiro.";
			mes "Voce quer se candidatar ao cargo?";
			next;
			if(select("Me de algum tempo para pensar.:Com certeza!") == 1) {
				mes "[Mestre Miller]";
				mes "Compreensível.";
				mes "Se voce decidir que quer se tornar um Justiceiro, entao me conte imediatamente.";
				mes "Eu te ajudarei a começar.";
				close;
			}
			mes "[Mestre Miller]";
			mes "Ótimo, ótimo.";
			mes "Muito bem, entao vamos começar.";
			mes "Leve esta carta para o Sr. Sábio Chifre de Touro, em Payon.";
			mes "Ele é um xama que irá julgar se voce é ou nao qualificado para se tornar um Justiceiro.";
			set GUNS_Q,1;
			setquest 6020;
			close;
		}
		else if (GUNS_Q == 1) {
			mes "[Mestre Miller]";
			mes "Leve essa carta de apresentaçao que escrevi para o Sr. Sábio Chifre de Touro, em Payon.";
			mes "Ele te testará para ver se voce pode se tornar um Justiceiro.";
			close;
		}
		else if (GUNS_Q == 2) {
			mes "[Mestre Miller]";
			mes "Hmm... o Sábio Chifre de Touro te pediu para coletar os itens que precisa para fazer o amuleto?";
			mes "Hm. Eu acho que isso faz parte do teste.";
			close;
		}
		else if (GUNS_Q == 3) {
			mes "[Mestre Miller]";
			mes "O Sábio Chifre de Touro te pediu um pouco de leite?";
			mes "Ele deve gostar de voce, já que está pedindo favores.";
			mes "Boa sorte, amigo.";
			close;
		}
		else if (GUNS_Q == 4) {
			mes "[Mestre Miller]";
			mes "Eu espero ouvir boas notícias de voce em breve.";
			mes "Voce sabe, eu nao tenho dúvida que voce irá se tornar um Justiceiro.";
			close;
		}
		else if (GUNS_Q == 5) {
			if (SkillPoint != 0) {
				mes "[Mestre Miller]";
				mes "Hey, voce tem Pontos de Habilidades sobrando.";
				mes "Use-os antes de vir falar comigo de novo.";
				close;
			}
			mes "[Mestre Miller]";
			mes "Oh, voce me trouxe um amuleto do Sábio Chifre de Touro?";
			mes "Já tem um tempo que ele nao dá um pra ninguém...";
			mes "Estou muito orgulhoso de voce!";
			next;
			mes "[Mestre Miller]";
			mes "Se o Sábio Chifre de Touro aprova, entao eu nao tenho motivos para rejeitar voce.";
			mes "Tudo bem entao, vou promove-lo a Justiceiro.";
			mes "Mas primeiro, deixe-me explicar sobre nossa classe mais detalhadamente.";
			next;
			mes "[Mestre Miller]";
			mes "Como um Justiceiro, voce deve manter sua arma com voce o tempo todo.";
			mes "A Guilda dos Justiceiros mantém o controle de todas as Armas e Projéteis, entao voce só pode obte-las dos membros da nossa guilda.";
			next;
			mes "[Mestre Miller]";
			mes "Nao se preocupe, os membros da Guilda dos Justiceiros podem ser encontrados em quase todos os lugares.";
			mes "Enfim, tem que ser desta maneira, por ordem da nossa líder da guilda, Lady Selena.";
			next;
			mes "[Mestre Miller]";
			mes "Voce pode ter a chance de conhece-la um dia desses.";
			mes "De qualquer forma, agora temos que controlar a venda de armas e projéteis para mante-los longe de pessoas más ou irresponsáveis.";
			next;
			mes "[Mestre Miller]";
			mes "De qualquer forma, para mim é sempre um prazer conversar com outro Justiceiro, entao mantenha contato.";
			mes "Que o poder da Terra te proteja em todas as suas aventuras~";
			callfunc "Job_Change",Job_Gunslinger;
			set GUNS_Q,6;
			completequest 6024;
			if (rand(1,2) == 1) {
				getitem 13100,1; // Six_Shooter
			} else {
				getitem 13150,1; // Branch
			}
			close;
		}
	} else {
		if (Class == Job_Gunslinger) {
			mes "[Mestre Miller]";
		mes "Oh! A quanto tempo nao te vejo, amigo. Como vai?";
		mes "Espero que voce tenha mantido sua arma bem cuidada.";
		mes "Cuide dela, e ela cuidará de voce. Lembre-se disso.";
			close;
		} else {
			mes "[Mestre Miller]";
			mes "Se voce nao tem nenhum assunto a tratar comigo, entao, por favor, siga seu caminho.";
			close;
		}
	}
}

payon,184,65,3	script	Sábio Chifre de Touro	866,{
	if (GUNS_Q == 1) {
		mes "[Sábio Chifre de Touro]";
		mes "Olá, jovem lobo. Que assuntos te trazem r minha presença?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "O Sr. Miller me mandou entregar esta carta a voce.";
		mes "Na verdade, estou interessado em me tornar um Justiceiro...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Miller, voce disse?";
		mes "Hm, o Raposa Negra nao costuma escrever apresentaçoes para qualquer um que ele nao acredita que será um bom Justiceiro";
		mes "Sim, eu acho que sei por que ele o mandou a mim.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Posso ver em seus olhos que tem um bom coraçao e um forte senso de responsabilidade.";
		mes "Tudo o que precisa é da bençao da Terra para te proteger como Justiceiro.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu posso fazer um amuleto que demonstrará seu desejo em se tornar um guerreiro da Terra, para que o apresente ao Mestre dos Justiceiros.";
		mes "Vou precisar destes itens...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "^3355FF1 Tronco^000000,";
		mes "^3355FF3 Felpas^000000,";
		mes "^3355FF3 Zargônios^000000,";
		mes "^3355FF10 Cascas^000000,";
		mes "^3355FF3 Ervas Verdes^000000,";
		mes "e ^3355FF3 Cascos Arco-Íris^000000.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Depois que eu terminar o amuleto, voce deve levá-lo ao Raposa Negra, e ele irá o ajudar a alcançar seu objetivo de se tornar um Justiceiro.";
		set GUNS_Q,2;
		changequest 6020,6021;
		close;
	}
	else if (GUNS_Q == 2) {
		if (countitem(912) < 3 || countitem(914) < 3 || countitem(1019) < 1 || countitem(935) < 10 || countitem(511) < 3 || countitem(1013) < 3) {
			mes "[Sábio Chifre de Touro]";
			mes "Eu posso fazer um amuleto que demonstrará seu desejo em se tornar um guerreiro da Terra, para que o apresente ao Mestre dos Justiceiros.";
			mes "Vou precisar destes itens...";
			next;
			mes "[Sábio Chifre de Touro]";
			mes "^3355FF1 Tronco^000000,";
			mes "^3355FF3 Felpas^000000,";
			mes "^3355FF3 Zargônios^000000,";
			mes "^3355FF10 Cascas^000000,";
			mes "^3355FF3 Ervas Verdes^000000,";
			mes "e ^3355FF3 Cascos Arco-Íris^000000.";
			next;
			mes "[Sábio Chifre de Touro]";
			mes "Depois que eu terminar o amuleto, voce deve levá-lo ao Raposa Negra, e ele irá o ajudar a alcançar seu objetivo de se tornar um Justiceiro.";
			close;
		}
		delitem 912,3; // Zargon
		delitem 914,3; // Fluff
		delitem 1019,1; // Wooden_Block
		delitem 935,10; // Shell
		delitem 511,3; // Green_Herb
		delitem 1013,3; // Colorful_Shell
		set GUNS_Q,3;
		changequest 6021,6022;
		mes "[Sábio Chifre de Touro]";
		mes "Ah, voce voltou com tudo que eu preciso.";
		mes "Por favor, me de algum tempo para fazer o amuleto.";
		mes "Se voce voltar daqui a pouco, eu devo ter terminado.";
		close;
	}
	else if (GUNS_Q == 3) {
		mes "[Sábio Chifre de Touro]";
		mes "Oh, voce chegou na hora.";
		mes "Já faz um tempo que nao faço um desses amuletos, entao devo estar um pouco enferrujado.";
		mes "Ainda assim, isso realmente me leva de volta para os dias de minha juventude.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu estive servindo nesta posiçao de escolher pessoas dignas do amuleto do Justiceiro por algumas décadas.";
		mes "Mas antes disso, eu era um jovem aventureiro, como voce.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Parece que foi ontem que eu ganhei meu próprio amuleto do Justiceiro, um guerreiro da Terra.";
		mes "Foi quando eu conheci o pai de Selena.";
		mes "Como o tempo pode passar tao rápido?";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Ah...";
		mes "Eu realmente aprecio toda a ajuda de Selena e do Raposa Negra em ajuda em recrutar jovens Justiceiros.";
		mes "Eu estou bastante velho agora, e nao consigo fazer tudo sozinho.";
		mes "*Suspiro...*";
		mes "Assim é a vida.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Antes de voce ir, posso pedir um pequeno favor?";
		mes "Estou com sede, e eu gostaria de um copo de leite gelado.";
		mes "Voce poderia me trazer algum?";
		set GUNS_Q,4;
		changequest 6022,6023;
		close;
	}
	else if (GUNS_Q == 4) {
		if (countitem(519) < 1) {
			mes "[Sábio Chifre de Touro]";
			mes "[Sábio Chifre de Touro]";
			mes "Eu sou um velho homem que, em breve, se reunirá com a Mae Terra.";
			mes "Voce faria um favor a este velho Justiceiro de me trazer um copo de ^3131FFleite^000000 gelado, por favor?";
			close;
		}
		delitem 519,1; // Milk
		set GUNS_Q,5;
		changequest 6023,6024;
		mes "[Sábio Chifre de Touro]";
		mes "Oh, obrigado por sua generosidade!";
		mes "Eu vejo que voce me trouxe um pouco de leite.";
		mes "Ahhhh, delícia...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu admiro a paciencia, gentileza, e bondade que voce provou trazendo isto para mim.";
		mes "Sim, essas sao características que todos nós queremos ter.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Agora, por favor, leve esse amuleto para Miller, o Raposa Negra, com toda minha aprovaçao.";
		mes "Eu espero que voce use sua arma para apoiar a justiça como um nobre guerreiro da Terra.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	}
	else if (GUNS_Q == 5) {
		mes "[Sábio Chifre de Touro]";
		mes "Por favor, leve esse amuleto para Miller, o Raposa Negra, com toda a minha aprovaçao.";
		mes "Eu espero que voce use sua arma para apoiar a justiça como um nobre guerreiro da Terra.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	}
	else if (GUNS_Q == 6) {
		mes "[Sábio Chifre de Touro]";
		mes "Faz muito tempo que nao te vejo.";
		mes "Espero que tenha se tornado uma fera inteligente, que usa seus poderes como Justiceiro para protecer o que é bom e justo.";
		close;
	}
	else {
		mes "[Sábio Chifre de Touro]";
		mes "Zzzzzz~";
		mes "^333333*Ufa*^000000";
		close;
	}
}