//===== rAthena Script ======================================= 
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//= 1.6.1 Tradução PT-BR [cleyton]
//= 1.6.2 Revisão [Holy]
//= 1.6.3 Revisão rAthena [HD Scripts]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[Corp. Eventos Incríveis]";
	mes "Bem-vindo a Corporação Eventos Incríveis.";
	mes "Nossa equipe está sempre trabalhando";
	mes "para superar suas expectativas";
	mes "a respeito da qualidade de serviço. Então";
	mes "como posso ajudá-lo hoje?";
	next;
	switch(select(getarg(0))) {
	case 1:
		mes "[Corp. Eventos Incríveis]";
		mes "Seu ponto de retorno foi gravado aqui no vilarejo de Hugel.";
		mes "Obrigado por usar o serviço da Corp. Eventos Incríveis";
		mes getarg(1)+".";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close2;
		break;
	case 2:
		mes "[Corp. Eventos Incríveis]";
		if(!callfunc("F_CanOpenStorage")){
			mes "Eu sinto muito, mas você precisa ser um Aprendiz com nível 6 de habilidade para poder usar o serviço de armazenamento.";
			close2;
			break;
		}
		if(Zeny < 40){
			mes "Eu lamento, mas você não tem dinheiro suficiente para usar o serviço de armazenamento.";
			mes "Para usar o armazenamento, o custo é de 40 zenys.";
			close2;
			break;
		}
		set Zeny, Zeny - 40;
		mes "Deixe-me abrir seu armazém pessoal agora.";
		mes "Obrigado por usar os serviços da Corp. Eventos Incríveis.";
		mes "Tenha um ótimo dia!~";
		callfunc("F_CheckKafCode");	//verifica sua senha do Armazém, caso possua.
		close2;
		openstorage;
		break;
	case 3:
		mes "Por favor escolha";
		mes "seu destino.";
		next;
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE))
			set .@cost,4400;
		else
			set .@cost,2200;
		.@destination$ = getarg(5);
		if (select(.@destination$ +" -> "+ .@cost +" z:Cancelar") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060,1; //Warp_Free_Ticket
				}
				else {
					set Zeny,Zeny-.@cost;
				}
				if (.@destination$ == "Veins") warp "veins",205,101;
				else if (.@destination$ == "Rachel") warp "rachel",115,125;
				cutin "",255;
				end;
			}
			else {
				mes "[Corp. Eventos Incríveis]";
				mes "Eu lamento, mas você não tem dinheiro o suficiente para usar o serviço de teleporte.";
				mes "Para "+ .@destination$ +" custam "+.@cost+" zeny.";
			}
		}
		close2;
		break;
	case 4:
		mes "[Corp. Eventos Incríveis]";
		if(BaseClass != Job_Merchant){
			mes "Eu lamento, mas o serviço de aluguel de carrinhos";
			if (checkre(0))
				mes "só está disponível para Mercadores.";
			else {
				mes "só está disponível para Mercadores, Ferreiros, Mestre-Ferreiros, Alquimistas, Criadores e suas evoluções.";
			}
			close2;
			break;
		}
		if(getskilllv("MC_PUSHCART") == 0){
			mes "Você só pode alugar um carrinho se possuir a abilidade 'Usar Carrinho'.";
			close2;
			break;
		}
		if(checkcart() == 1){
			mes "Você já tem um carrinho de mão equipado.";
			mes "Infelizmente, só podemos alugar um carrinho de mão por vez.";
			close2;
			break;
		}
		if(countitem(7061)){
			delitem 7061,1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "O aluguel do carrinho de mão custa 800 zenys. Você deseja alugar o carrinho de mão?";
		next;
		if(select("Alugar um Carrinho:Cancelar") == 1){
			if(Zeny < 800){
				mes "[Corp. Eventos Incríveis]";
				mes "Eu lamento, mas você não possui zenys para pagar o aluguel do carrinho de mão que custa 800 zenys.";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[Corp. Eventos Incríveis]","Corp. Eventos Incríveis");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[Corp. Eventos Incríveis]";
		mes "A Corp. Eventos Incríveis, está sempre se esforçando para providenciar o melhor serviço para nossos clientes.";
		mes "Ajude-nos a nos tornar os melhores usando nossos serviços com frequência. Muito obrigado!~";
		close2;
	}
	cutin "", 255;
	end;
}

hugel,88,168,4	script	Cool Event Corp. Staff::CoolEventCorpStaffHugel	874,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazém:Serviço de Teleporte:Alugar um Carrinho:Serviço Armazém com senha:Cancelar",
		"na vila de Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	Cool Event Corp. Staff::CoolEventCorpStaffRachel	874,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazém:Serviço de Teleporte:Alugar um Carrinho:Serviço Armazém com senha:Cancelar",
		"na cidade de Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Cool Event Corp. Staff::CoolEventCorpStaffVeins	874,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazém:Serviço de Teleporte:Alugar um Carrinho:Serviço Armazém com senha:Cancelar",
		"na cidade de Veins","veins",204,103,"Rachel";
	end;
}
